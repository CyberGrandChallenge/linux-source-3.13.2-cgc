#! /usr/bin/perl
#                              -*- Mode: Perl -*- 
# image.prerm --- 
# Author           : root ( root@melkor.pilgrim.umass.edu ) 
# Created On       : Fri May 17 03:28:59 1996
# Created On Node  : melkor.pilgrim.umass.edu
# Last Modified By : Manoj Srivastava
# Last Modified On : Mon Apr 13 14:23:29 2009
# Last Machine Used: anzu.internal.golden-gryphon.com
# Update Count     : 86
# Status           : Unknown, Use with caution!
# HISTORY          : 
# Description      : 
# 
#
#    $Id: image.prerm,v 1.22 2003/10/07 16:24:20 srivasta Exp $
#
# 
#use strict;
use Debconf::Client::ConfModule qw(:all);
version('2.0');
my $capb=capb("backup");

$|=1;
# Predefined values:
my $version            = "=V";
my $move_image         = '';       # target machine defined
my $kimage             = "=K";	# Should be empty, mostly
my $image_dir          = "=D";     # where the image is located
my $clobber_modules    = '';       # target machine defined
my $initrd             = "=I";     # initrd kernel
my $postinst_hook      = '';       #Normally we do not
my $postrm_hook        = '';       #Normally we do not
my $preinst_hook       = '';       #Normally we do not
my $prerm_hook         = '';       #Normally we do not
my $ignore_depmod_err  = '';	# normally we do not
my $relink_build_link  = 'YES';	# There is no harm in checking the link
my $force_build_link   = '';	# There is no harm in checking the link
my $arch               = "=A";  #  should be same as dpkg --print-installation-architecture
my $kernel_arch        = "=B";
my $package_name       = "=ST-image-$version";
my $kernel_pkg_version = "=KPV";


#known variables
my $image_dest      = "/";
my $realimageloc    = "/$image_dir/";
my $have_conffile   = "";
my $CONF_LOC        = '/etc/kernel-img.conf';
my $silent_modules  = '';
my $warn_reboot     = 'Yes';     # Warn that we are installing a version of
                                 # the kernel we are running

# remove multiple leading slashes; make sure there is at least one.
$realimageloc  =~ s|^/*|/|o;
$realimageloc  =~ s|/+|/|o;

my $DEBUG = 0;

# Variables used
my $image='';
my $ret=0;
my $seen='';
my $answer='';
my $running = '';
my $WouldInvalidate = 0;

if ($ARGV[0] && ($ARGV[0] =~ /remove/ || $ARGV[0] =~ /upgrade/)) {
  if (-l "/usr/doc/=ST-image-$version") {
    unlink "/usr/doc/=ST-image-$version";
  }
}

# Ignore all invocations uxcept when called on to remove
exit 0 unless ($ARGV[0] && $ARGV[0] =~ /remove/) ;

# Paranoid check to make sure that the correct value is put in there
if (! $kimage)                      { $kimage = "vmlinuz";} # Hmm. empty
elsif ($kimage =~ m/^b?zImage$/o)   { $kimage = "vmlinuz";} # these produce vmlinuz
elsif ($kimage =~ m/^[iI]mage$/o)   { my $nop = $kimage;  }
elsif ($kimage =~ m/^vmlinux$/o)    { my $nop = $kimage;  }
elsif ($kimage =~ m/^xen[0u]-linux$/o){
    $package_name       = "${kimage}-$version";}
else                                { $kimage = "vmlinuz";} # Default

if (-r "$CONF_LOC" && -f "$CONF_LOC"  ) {
  if (open(CONF, "$CONF_LOC")) {
    while (<CONF>) {
      chomp;
      s/\#.*$//g;
      next if /^\s*$/;

      $move_image        = "" if m/^\s*move_image\s*=\s*(no|false|0)\s*$/i;
      $clobber_modules   = '' if m/^\s*clobber_modules\s*=\s*(no|false|0)\s*$/i;
      $silent_modules    = '' if m/^\s*silent_modules\s*=\s*(no|false|0)\s*$/i;
      $warn_reboot       = '' if m/^\s*warn_reboot\s*=\s*(no|false|0)\s*$/i;
      $ignore_depmod_err = '' if m/^\s*ignore_depmod_err\s*=\s*(no|false|0)\s*$/i;
      $relink_src_link   = '' if m/^\s*relink_src_link\s*=\s*(no|false|0)\s*$/i;
      $relink_build_link = '' if m/^\s*relink_build_link\s*=\s*(no|false|0)\s*$/i;
      $force_build_link  = '' if m/^\s*force_build_link\s*=\s*(no|false|0)\s*$/i;

      $move_image        = "Yes" if m/^\s*move_image\s*=\s*(yes|true|1)\s*$/i;
      $clobber_modules   = "Yes" if m/^\s*clobber_modules\s*=\s*(yes|true|1)\s*$/i;
      $silent_modules    = 'Yes' if m/^\s*silent_modules\s*=\s*(yes|true|1)\s*$/i;
      $warn_reboot       = 'Yes' if m/^\s*warn_reboot\s*=\s*(yes|true|1)\s*$/i;
      $ignore_depmod_err = 'Yes' if m/^\s*ignore_depmod_err\s*=\s*(yes|true|1)\s*$/i;
      $relink_src_link   = 'Yes' if m/^\s*relink_src_link\s*=\s*(yes|true|1)\s*$/i;
      $relink_build_link = 'Yes' if m/^\s*relink_build_link\s*=\s*(yes|true|1)\s*$/i;
      $force_build_link  = 'Yes' if m/^\s*force_build_link\s*=\s*(yes|true|1)\s*$/i;

      $image_dest    = "$1"  if m/^\s*image_dest\s*=\s*(\S+)/i;
      $postinst_hook = "$1"  if m/^\s*postinst_hook\s*=\s*(\S+)/i;
      $postrm_hook   = "$1"  if m/^\s*postrm_hook\s*=\s*(\S+)/i;
      $preinst_hook  = "$1"  if m/^\s*preinst_hook\s*=\s*(\S+)/i;
      $prerm_hook    = "$1"  if m/^\s*prerm_hook\s*=\s*(\S+)/i;
      $mkimage       = "$1"  if m/^\s*mkimage\s*=\s*(.+)$/i;
      $ramdisk       = "$1"  if m/^\s*ramdisk\s*=\s*(.+)$/i;
    }
    close CONF;
    $have_conffile = "Yes";
  }
}


$ENV{KERNEL_ARCH}=$kernel_arch if $kernel_arch;

#check to see if we are trying to remove a running kernel
# if so we abort right now.
chop($running=`uname -r`);
if ($running eq $version) {
  my $question = "${package_name}/prerm/removing-running-kernel-$version";

  ($ret,$seen) = fset ("$question", 'seen', 'false');
  die "Error setting debconf flags in $question: $seen" if $ret;

  $ret = subst("$question", 'running', "$running");
  die "Error setting debconf substitutions in $question: $seen" if $ret;

  ($ret,$seen) = input('critical', "$question");
  if ($ret && $ret != 30 ) {
    die "Error setting debconf question $question: $seen";
  }

  ($ret,$seen) = go ();
  if ($ret && $ret != 30 ) {
    die "Error asking debconf question $question: $seen";
  }

  ($ret,$answer) = get("$question");
  die "Error retreiving answer for $question: $answer" if $ret;

  if ($answer =~ /^(y|t)/i) {
    exit 1; #Operation not permitted
  }
  else {
     print STDERR "Ok, proceeding with removing running kernel image.\n";
  }
}

#Now, they have an alternate kernel which they are currently running

# This is just us being nice to lilo users.

chdir("/") or die "could not chdir to /:$!\n";

# set the env var stem
$ENV{'STEM'} = "=ST";

sub exec_script {
  my $type   = shift;
  my $script = shift;
  print STDERR "Running $type hook script $script.\n";
  system ("$script $version $realimageloc$kimage-$version") &&
    print STDERR "User $type hook script [$script] ";
  if ($?) {
    if ($? == -1) {
      print STDERR "failed to execute: $!\n";
    }
    elsif ($? & 127) {
      printf STDERR "died with signal %d, %s coredump\n",
        ($? & 127),  ($? & 128) ? 'with' : 'without';
    }
    else {
      printf STDERR "exited with value %d\n", $? >> 8;
    }
    exit $? >> 8;
  }
}
sub run_hook {
  my $type   = shift;
  my $script = shift;
  if ($script =~ m,^/,) {
    # Full path provided for the hook script
    if (-x "$script") {
      &exec_script($type,$script);
    }
    else {
      die "The provided $type hook script [$script] could not be run.\n";
    }
  }
  else {
    # Look for it in a safe path
    for my $path ('/bin', '/sbin', '/usr/bin', '/usr/sbin') {
      if (-x "$path/$script") {
        &exec_script($type, "$path/$script");
        return 0;
      }
    }
    # No luck
    print STDERR "Could not find $type hook script [$script].\n";
    die "Looked in: '/bin', '/sbin', '/usr/bin', '/usr/sbin'\n";
  }
}

# Set up the env variable containing our arguments
my $out;
for (@ARGV) {
  s,','\\'',g;
  $out.=" '$_'";
}
$ENV{'DEB_MAINT_PARAMS'}="$out";
$ENV{'KERNEL_PACKAGE_VERSION'}="$kernel_pkg_version";

if (!$initrd) {
  $ENV{INITRD}='No';
}

## Run user hook script here, if any
if (-d "/etc/kernel/prerm.d") {
  print STDERR "Examining /etc/kernel/prerm.d.\n";
  system ("run-parts --verbose --exit-on-error --arg=$version " . 
          "--arg=$realimageloc$kimage-$version /etc/kernel/prerm.d") &&
            die "Failed to process /etc/kernel/prerm.d";
}
if (-d "/etc/kernel/prerm.d/$version") {
  print STDERR "Examining /etc/kernel/prerm.d/$version.\n";
  system ("run-parts --verbose --exit-on-error --arg=$version" .
          " --arg=$realimageloc$kimage-$version " .
          "/etc/kernel/prerm.d/$version") &&
            die "Failed to process /etc/kernel/prerm.d/$version";
}

if (-x "$prerm_hook") {
  &run_hook("prerm", $prerm_hook);
}

sub success () {
    -f "/lib/modules/$version/modules.dep"  && 
      unlink "/lib/modules/$version/modules.dep";
    exit 0;
}



&success();
exit 0;
__END__





